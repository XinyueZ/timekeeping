buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


def buildNumber = 'bld' + (System.env.BUILD_NUMBER ?: 'SNAPSHOT')

android {
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        multiDexEnabled = true
        applicationId project.APP_APPLICATIONID
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.APP_VERSIONCODE)
        versionName project.APP_VERSIONNAME
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "2g"
    }

    buildTypes {
        debug {
            versionNameSuffix buildNumber
        }
        release {
            shrinkResources true
        }
    }

    productFlavors {
        dev {
            applicationId project.APP_STAGE_APPLICATIONID
            minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
            targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
            versionCode Integer.parseInt(project.APP_VERSIONCODE)
            versionName project.APP_VERSIONNAME
        }
        live {
            applicationId project.APP_APPLICATIONID
            minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
            targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
            versionCode Integer.parseInt(project.APP_VERSIONCODE)
            versionName project.APP_VERSIONNAME
        }
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.android.gms:play-services:8.3.0'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'joda-time:joda-time:2.3'
    compile 'com.code-troopers.betterpickers:library:2.2.1'
    compile project(':volley_cz')
    compile project(':ChoppingBootstrap')
    compile 'com.github.hotchemi:permissionsdispatcher:1.2.1@aar'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:1.2.1'
    compile 'com.facebook.stetho:stetho-okhttp:1.1.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }
    compile 'io.realm:realm-android:0.85.1'
}


// Rename final apks
android.applicationVariants.all {  variant ->
    variant.outputs.each  { output ->
        output.outputFile = new File(
                output.outputFile.parent, output.outputFile.name.replace("_Android", "")
                .replace(".apk", "-${android.defaultConfig.versionName}${buildNumber}.apk"))
    }
}
